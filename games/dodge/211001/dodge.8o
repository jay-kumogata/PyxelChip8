# Octojam8サンプルを改造(StarForce風のキャラクタに変更)
# ひたすら隕石をよける(dodge)ゲーム(2021年9月11日)

: main
	# レジスタを初期化
	i := registers
	load v4
	clear

	# 宇宙船表示: あたり判定後にループ
	i := ship
	sprite v0 v1 4

	# メインループ
	loop
	
		# ディレイループ: 1周する時間が一定になるように待つ
		loop
			vf := delay
			if vf != 0 then
		again
		vf := 10
		delay := vf
		
		vf := random 3
		if vf == 0 begin
			# 岩表示: 乱数で表示位置を決める
			vf := random 63
			i := rock
			sprite vf v2 4	
		end

		# 宇宙船消去
		i := ship
		sprite v0 v1 4
							
		# 宇宙船移動
		if v3 key then v0 -= 2
		if v4 key then v0 += 2

		# スクロールダウン: 宇宙船の高さ(4ピクセル)以上が必要
		scroll-down 1
		
		# 宇宙船表示: あたり判定後にループ
		i := ship
		sprite v0 v1 4
		
		if vf == 0 then
	again

	# ゲームオーバー: キーを押すと再ゲーム
	vf := key
	jump main
	
: registers
	32
	28
	 0
	:byte OCTO_KEY_A
	:byte OCTO_KEY_D

# 宇宙船
: ship 0x14 0x36 0x2A 0x36
# 岩
: rock 0x60 0xD0 0xB0 0x60