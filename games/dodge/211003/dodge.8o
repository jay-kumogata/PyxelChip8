#############################################################
# 
# Dodge game for Chip8 (based on a Octojam8 sample)
# 
# Operate the ship and avoid the rocks.
# 
# Sep 11, 2021 ver.1 (changed to StarForce-like character)
#
#############################################################

: main
	# Initialize registers
	i := registers
	load v4
	clear

	# Draw ship
	i := ship
	sprite v0 v1 4

	# Main loop
	loop
	
		# Delay loop: Wait for one loop to be a certain time
		loop
			vf := delay
			if vf != 0 then
		again
		vf := 10
		delay := vf
		
		vf := random 3
		if vf == 0 begin
			# Draw rock: Determine position with random numbers
			ve := random 63
			i := rock
			sprite ve v2 4		
		end
		
		# Erase ship
		i := ship
		sprite v0 v1 4
							
		# Move ship
		if v3 key then v0 -= 2
		if v4 key then v0 += 2

		# Scroll down 
		scroll-down 1
		
		# Draw ship: Loop after hit judgment
		i := ship
		sprite v0 v1 4
		
		if vf == 0 then
	again

	# Game over: Press key to replay game
	vf := key
	jump main
	
: registers
	32
	28
	0
	:byte OCTO_KEY_A
	:byte OCTO_KEY_D

# Character patterns
: ship 0x2A 0x6B 0x5D 0x6B 
: rock 0x60 0xD0 0xB0 0x60
