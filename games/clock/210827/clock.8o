# Just a simple stopwatch example using the delay timer.
#
# Coded by Jeffrey Massung as an example of the
# CHIP-8 assembler.
#
# Have fun!
#


: main
    #    super
    hires 

    # minute and second
    v4 := 0
    v5 := 0

: myloop

    # show the current time
    draw_clock

    # wait for a full second
    v0 := 60
    delay := v0

: wait

    # wait until 1 second has elapsed
    v0 := delay
    if v0 != 0 then jump wait

    # erase the old time
    draw_clock

    # play a tick sound
    v0 := 1
    buzzer := v0

    # add a second
    v5 += 1
    if v5 != 60 then jump myloop

    # add a minute
    v5 := 0
    v4 += 1

    # done
    jump myloop

: draw_clock

    # load minute
    i := time
    bcd     v4
    load v2

    # draw tens
    va := 12
    vb := 10

    i := bighex v1
    sprite va vb 10

    # draw ones
    va += 10
    i := bighex v2
    sprite va vb 10

    # bcd second
    i := time
    bcd     v5
    load v2

    # draw tens
    va += 14
    i := bighex v1
    sprite va vb 10

    # draw ones
    va += 10
    i := bighex v2
    sprite va vb 10

    # done
    ;

: time

    0 0 0